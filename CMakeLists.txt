PROJECT ( MESHUP )

CMAKE_MINIMUM_REQUIRED (VERSION 3.0)

# Needed for UnitTest++
LIST( APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/CMake )

# Perform the proper linking
SET (CMAKE_SKIP_BUILD_RPATH FALSE)
SET (CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)
SET (CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")
SET (CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

SET (MESHUP_VERSION_MAJOR 0)
SET (MESHUP_VERSION_MINOR 5)
SET (MESHUP_VERSION_PATCH 0)
SET (MESHUP_VERSION_STRING "${MESHUP_VERSION_MAJOR}.${MESHUP_VERSION_MINOR}.${MESHUP_VERSION_PATCH}")
SET (MESHUP_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})

# Get VCS information
EXECUTE_PROCESS (COMMAND "hg" "id" "-i"
	OUTPUT_VARIABLE VCS_ID
	RESULT_VARIABLE VCS_RESULT
	)

IF (NOT VCS_RESULT)
	STRING(REGEX REPLACE "(\r?\n)+$" "" VCS_ID "${VCS_ID}")
	SET (MESHUP_VERSION_STRING "${MESHUP_VERSION_STRING} (${VCS_ID})")
ENDIF (NOT VCS_RESULT)

# The QT Interface
SET( QT_USE_QTXML TRUE )
SET( QT_USE_QTOPENGL TRUE )
SET( QT_WRAP_CPP TRUE )
FIND_PACKAGE (Qt5Widgets REQUIRED)
FIND_PACKAGE (Qt5OpenGL)
FIND_PACKAGE (OpenGL)
FIND_PACKAGE (Boost COMPONENTS filesystem system REQUIRED)

INCLUDE_DIRECTORIES ( 
	vendor/glew/include 
	vendor/lua-5.1/src/
	vendor/QTFFmpegWrapper/src/
	src/luatables
	${CMAKE_CURRENT_BINARY_DIR}/src/
	)

SUBDIRS ( 
	tests/ 
	vendor/jsoncpp/
	vendor/lua-5.1/
	vendor/QTFFmpegWrapper/
	)

ADD_LIBRARY ( glew
	vendor/glew/src/glew.c
	vendor/glew/src/glewinfo.c
	vendor/glew/src/visualinfo.c
	)

ADD_DEFINITIONS (${QT_DEFINITIONS})

SET ( MainWindow_UIS
	ui/MainWindow.ui
	ui/RenderImageDialog.ui
	ui/RenderImageSeriesDialog.ui
	)

QT5_WRAP_UI (MainWindow_UIS_H ${MainWindow_UIS})

SET ( MeshupApp_SRCS
	src/main.cc
	src/glwidget.cc
	src/MeshupApp.cc
	)

QT5_WRAP_CPP ( MeshupApp_MOC_SRCS
	src/MeshupApp.h
	src/RenderImageDialog.h
	src/RenderImageSeriesDialog.h
	src/glwidget.h
	)

ADD_EXECUTABLE ( meshup
	src/Model.cc
	src/Animation.cc
	src/MeshVBO.cc
	src/Curve.cc
	src/ForcesTorques.cc
	src/Scene.cc
	src/Camera.cc
	src/Scripting.cc
	src/Arrow.cc
	src/luatables/luatables.cc

	${MeshupApp_SRCS}
	${MeshupApp_MOC_SRCS}
	${MainWindow_UIS_H}
)


INCLUDE_DIRECTORIES (${QT_INCLUDE_DIR})
INCLUDE_DIRECTORIES (${QT_INCLUDE_DIR}/QtOpenGL)
INCLUDE_DIRECTORIES (${CMAKE_CURRENT_BINARY_DIR})
INCLUDE_DIRECTORIES (${CMAKE_CURRENT_SOURCE_DIR})
INCLUDE_DIRECTORIES (${CMAKE_CURRENT_SOURCE_DIR}/src)
INCLUDE_DIRECTORIES ( include )
INCLUDE_DIRECTORIES ( vendor/jsoncpp/include )

CONFIGURE_FILE (
	"${CMAKE_CURRENT_SOURCE_DIR}/src/meshup_config.h.cmake"
	"${CMAKE_CURRENT_BINARY_DIR}/src/meshup_config.h"
	)

SET_TARGET_PROPERTIES ( ${PROJECT_EXECUTABLES} PROPERTIES
	LINKER_LANGUAGE CXX
	INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib" 
)

#qt5_use_modules(meshup Widgets Core OpenGL)
TARGET_LINK_LIBRARIES ( meshup
	${Qt5Widgets_LIBRARIES}
	${Qt5Core_LIBRARIES}
	${Qt5OpenGL_LIBRARIES}
	${QT_LIBRARIES}
	${OPENGL_LIBRARIES}
	${Boost_LIBRARIES}
	lua-static
	glew
	json
	qtffmpeg
)

# Installation
INSTALL (TARGETS meshup
RUNTIME DESTINATION bin
LIBRARY DESTINATION lib
)

FILE ( GLOB model_files
	models/*.lua
	)

FILE ( GLOB mesh_files
	meshes/*.obj
	)

INSTALL ( FILES ${model_files} DESTINATION share/meshup/models )
INSTALL ( FILES ${mesh_files} DESTINATION share/meshup/meshes )

# Packaging
SET(CPACK_GENERATOR "DEB")
SET(CPACK_DEBIAN_PACKAGE_SECTION "science")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Martin Felis <martin.felis@iwr.uni-heidelberg.de>")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libqt5opengl5, libglu1-mesa (>=8.0), libboost-filesystem1.48.0 (>= 1.48.0-1)")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "MeshUp - a visualization tool for multi-body systems based on skeletal animation and magic.")
SET(CPACK_PACKAGE_VENDOR "Martin Felis")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
SET(CPACK_PACKAGE_VERSION_MAJOR ${MESHUP_VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${MESHUP_VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${MESHUP_VERSION_PATCH})
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "CPACK_PACKAGE ${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}")
SET(CPACK_PACKAGE_FILE_NAME "MeshUp-${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
IF(UNIX)
	SET(CPACK_STRIP_FILES "bin/meshup")
  SET(CPACK_SOURCE_STRIP_FILES "")
ENDIF(UNIX)
SET(CPACK_PACKAGE_EXECUTABLES "meshup;Meshup")

INCLUDE(CPack)
